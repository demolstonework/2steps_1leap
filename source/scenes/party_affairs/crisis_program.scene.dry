title: The Economic Crisis
new-page: true
is-card: true
view-if: crisis_urgency >= 1 and crisis_program_timer = 0 and economic_plan = 0 and black_thursday_seen == 1
frequency: 200
on-arrival: month_actions += 1; crisis_program_timer = 5
card-image: img/bankrun.jpg
tags: party_affairs, govt_affairs

= Addressing the Economic Crisis

Our party is wracked as to how to deal with the economic crisis. From the centrist faction, Hilferding argues that the economic crisis must be allowed to play out, because it is simply a part of the cyclic nature of capitalism. [? if centrist_reform >= 3: Yet even within his own faction there are opposing voices declaring that the core principle of centrist Marxism is unity, not inaction. ?] [? if (centrist_reform >= 3 and fr_uk_seen and scandi_seen and austria_seen and comintern_seen and us_seen): Indeed, our efforts to forge bonds with socialist and communist parties all over the globe have inspired many to call for the formation of a United International. ?]

From labor, the economist Wladimir Woytinsky argues that we must pursue a massive public works program to reduce unemployment and stimulate demand, perhaps including deficit spending. His opponents, like Hilferding, argue that this plan will cause a return to hyper-inflation, and that it would not even help the economy. In addition, according to Hilferding, Woytinsky's strategy is opposed to Marxist principles. The so-called WTB plan, after its chief supporters Woytinsky, Tarnow, and Baade, is receiving more attention within the party.

However, leftists in the party argue that the WTB plan is merely a bandage over the fundamental flaws of capitalism. They argue that the crisis is an opportunity to end capitalism through massive nationalizations of industry, and finally bring our country to socialism.

Meanwhile, moderates and reformists draw up proposals for a limited plan of job creation through public works, funded through taxes, that does not involve deficit spending or large-scale restructuring of the economy. Labor argues that the plan is inadequate in its scale and ambition. [? if (neorevisionism > 0 and eu = 0): Finally, the new Neo-Revisionist faction seeks to address the crisis by strengthening economic and political ties to other European nations in order to lessen the burden on ours. ?]

- @support_centrist: Support the Centrist view.
- @support_true_centrist: Support the other Centrist view.
- @support_labor: Support the Labor view.
- @support_left: Support the Left view.
- @support_moderate: Support the Reformist view.
- @support_neorev: Support the Neo-Revisionist view.
- @defer: Defer making a decision until we have more information.

@support_centrist
subtitle: [? if finance_minister_party == "SPD": Finance Minister Hilferding's response is, as always, more austerity. ?][? if finance_minister_party != "SPD": Let the depression play itself out. ?]
on-arrival: center_strength += 5; center_dissent -= 5; labor_dissent += 10; wtb_support -= 1; left_dissent += 10; centrist_reform +=0.3;

Woytinsky and the trade unions vehemently criticize the centrists for their policy. The workers are starving; are we going to callously abandon them and side with bourgeois austerity? On the other hand, the centrists criticize Woytinsky for promising the workers false hope; their position is that nothing can be done to alleviate the depression. They also speak of the fear of another round of hyperinflation that may result from deficit spending.

Our policy course is currently that nothing can be done to alleviate the depression.

@support_labor
subtitle: Call for increased public works and fiscal stimulus.
unavailable-subtitle: The Labor faction is not strong enough.
choose-if: historical_mode == 0 or (labor_strength >= left_strength and labor_strength >= center_strength and labor_strength >= reformist_strength)
on-arrival: center_dissent += 10; left_dissent += 5; labor_strength += 5; wtb_support += 1;
go-to: plan_adopted if wtb_support >= 3; plan_support if wtb_support < 3

@plan_support

Our support of the WTB plan alienates the orthodox Marxists of the Centrist faction, and faces both opposition and support from a variety of sources across the spectrum. Leftists argue that the plan is flawed because it leaves capitalism intact, while some reformists are cautiously optimistic.

We have not yet convinced the party to formally adopt the plan. More work needs to be done.

@plan_adopted
on-arrival: wtb_adopted = 1; labor_dissent -= 15; reformist_dissent -= 10; labor_strength += 10; center_strength -= 10; center_dissent += 20; left_dissent += 5; economic_plan = 1

With our hard work, we have finally convinced the majority of the party to adopt the WTB plan! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?][? if spd_in_government = 0 : We should emphasize the plan in our electoral propaganda. ?]

@support_left
subtitle: Call for mass nationalizations and the transformation of the economy.
unavailable-subtitle: The Left faction is not strong enough.
choose-if: historical_mode == 0 or (left_strength >= labor_strength and left_strength >= center_strength and left_strength >= reformist_strength)
on-arrival: left_strength += 10; reformist_dissent += 5; labor_dissent += 5; nationalization_support += 2
go-to: support_left_progress if nationalization_support < 3; adopt_left if nationalization_support >= 3

@support_left_progress

We attempt to convince the party to support a program of mass nationalizations in response to the economic crisis. Leftists argue that this policy is in accordance with our long-standing socialist platform, while reformists and some trade unionists argue that the plan is unrealistic, and that we need to focus on more immediate improvements to workers' lives.

We have not yet formally adopted the plan. More work needs to be done.

@adopt_left
on-arrival: left_strength += 15; left_dissent -= 10; reformist_dissent += 10; neorevisionist_dissent += 8; labor_dissent += 6; nationalization_adopted = 1; coup_progress += 1; economic_plan = 3; kpd_relation += 5*(1-dissent)

With our hard work, we have convinced the majority of the party to adopt the socialist economic plan! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?] Bourgeois opposition is likely to be fierce. [? if spd_in_government == 0 : It might be difficult to enact the plan, given present conditions. ?]

@support_moderate
subtitle: Call for a limited program of job creation without deficit spending.
unavailable-subtitle: The Reformist faction is not strong enough.
choose-if: historical_mode == 0 or (reformist_strength >= left_strength and reformist_strength >= center_strength and reformist_strength >= labor_strength)
on-arrival: moderate_plan_support += 1; reformist_strength += 8; reformist_dissent -= 5; left_dissent += 6; labor_dissent += 5
go-to: adopt_moderate if moderate_plan_support >= 2; support_moderate_progress if moderate_plan_support < 2

@support_moderate_progress

We attempt to convince the party to support a moderate program of job creation without deficit spending. Advocates of the moderate plan argue that this plan is most likely to receive support from the bourgeois parties, and most practical in the immediate term. Opponents in the left and labor factions criticize the plan for being inadequate in its ambition.

We have not yet adopted the plan. More work needs to be done.

@adopt_moderate
on-arrival: reformist_strength += 10; reformist_dissent -= 10; left_dissent += 8; labor_dissent += 6; moderate_plan_adopted = 1; economic_plan = 2; coalition_dissent -= 1 if unemployed > 15; z_relation += 5;

With our hard work, we have convinced the majority of the party to adopt the moderate job creation plan! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries. ?]

@support_neorev
subtitle: Call for the formation of the European Union.
view-if: (neorevisionism >= 0 and eu = 0 and historical_mode = 0)
on-arrival: neorevplan_support += 1; neorevisionist_strength += 8; neorevisionist_dissent -= 5; left_dissent += 5; labor_dissent += 5; center_dissent += 5; reformist_dissent += 5;
go-to: adopt_neorev if neorevplan_support >= 2; support_neorev_progress if neorevplan_support < 2

@support_neorev_progress

We attempt to convince the party to support the creation of a European Union. Advocates of the plan argue that this plan will distribute the economic burden more equitably and bring long-term benefits. Opponents in other factions criticize the plan for being impractical and a deviation from Marxist principles.

We have not yet adopted the plan. More work needs to be done.

@adopt_neorev
on-arrival: neorevisionist_strength += 10; neorevisionist_dissent -= 10; neorevplan_adopted = 1; economic_plan = 5; left_dissent += 8; labor_dissent += 8; center_dissent += 5; reformist_dissent += 5;

With our hard work, we have convinced the majority of the party to adopt the neorevisionist plan to form the European Union! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Foreign ministry. ?]

@defer

We hold off on making a decision on economic policy, awaiting more information. Woytinsky and his supporters argue that inaction is a form of action of its own. 

################## Secret True Centrist Marxism Path

@support_true_centrist
subtitle: A global crisis demands a global response.
view-if: (centrist_reform >= 1 and historical_mode == 0)
choose-if: fr_uk_seen and scandi_seen and austria_seen and comintern_seen and us_seen
unavailable-subtitle: We need more connections to socialist and communist parties across the world. [? if not us_seen: Yes, even that one. ?]
on-arrival: center_strength += 6; center_dissent += 3; labor_dissent += 3; reformist_dissent += 1; true_centrist_support += 1.5
go-to: true_adopted if true_centrist_support >= 2; true_support if true_centrist_support < 2

@true_support

Only by through worker solidarity can we protect each other against capitalist depredations. Labor is somewhat critical of our efforts. The German workers are starving; are we going to #abandon them while we chase an utopian dream? On the other hand, traditionalist centrists lambast their colleagues for sowing discord under the guise of promoting unity.

@true_adopted
on-arrival: true_centrist_adopted = 1; labor_dissent += 5; reformist_dissent += 5; center_strength += 10; center_dissent -= 20; left_dissent -= 5; economic_plan = 4

With our hard work, we have finally convinced the majority of the party to adopt the Centrist plan of unity! [? if spd_in_government == 1 : We can begin implementing the plan as soon as possible, as long as we have the Economic or Finance ministries, as well as the Foreign ministry. ?] [? if spd_in_government == 0 : It might be difficult to enact the plan, given present conditions. ?]
